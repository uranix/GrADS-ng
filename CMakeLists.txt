cmake_minimum_required(VERSION 3.10)

project(GrADS VERSION 2.2.3 HOMEPAGE_URL jennifer.m.adams@nasa.gov LANGUAGES C)

set(CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/cmake" ${CMAKE_MODULE_PATH})

include(CheckIncludeFile)
include(CheckLibraryExists)
include(CheckFunctionExists)
include(CheckSymbolExists)

option(USEGADAP "GrADS specific library for accessing station data" OFF)
option(USEDAP "Use DAP" OFF)
option(USEGUI "Enable GUI" OFF)
option(USEREADLINE "Use readline" ON)
option(USEGEOTIFF "GeoTIFF support" OFF)
option(USESHP "Use shapefile" OFF)
option(USEGD "Use GD" OFF)
option(USECAIRO "Enable Cairo output" OFF)
option(USEGRIB2 "GRIB2 support" OFF)
option(USEHDF "HDF support" OFF)
option(USEHDF5 "HDF5 support" OFF)
option(USENC4 "NetCDF4 support" OFF)
option(USENC3 "NetCDF3 support" OFF)

if(CMAKE_C_BYTE_ORDER STREQUAL "BIG_ENDIAN")
  set(BYTEORDER 1)
elseif(CMAKE_C_BYTE_ORDER STREQUAL "LITTLE_ENDIAN")
  set(BYTEORDER 0)
else()
  set(BYTEORDER 0)
  message(WARNING "Endianness could not be determined.")
endif()

check_include_file("dlfcn.h" HAVE_DLFCN_H)
check_include_file("inttypes.h" HAVE_INTTYPES_H)
check_include_file("malloc.h" HAVE_MALLOC_H)
check_include_file("memory.h" HAVE_MEMORY_H)
check_include_file("stdint.h" HAVE_STDINT_H)
check_include_file("strings.h" HAVE_STRINGS_H)
check_include_file("string.h" HAVE_STRING_H)
check_include_file("sys/file.h" HAVE_SYS_FILE_H)
check_include_file("sys/stat.h" HAVE_SYS_STAT_H)
check_include_file("sys/types.h" HAVE_SYS_TYPES_H)
check_include_file("unistd.h" HAVE_UNISTD_H)
check_include_file("stdlib.h" HAVE_STDLIB_H)

if(USEREADLINE)
  find_package(Readline)
  if(READLINE_FOUND)
    set(USEREADLINE ON)
    list(APPEND grads_LIBS Readline::Readline)
  else()
    set(USEREADLINE OFF)
  endif()
endif()

check_function_exists("fseeko" HAVE_FSEEKO)
check_function_exists("SimpleGetFile" HAVE_SIMPLEGETFILE)

check_library_exists("m" "sqrt" "" HAVE_LIBM)
if(HAVE_LIBM)
  list(APPEND grads_LIBS m)
endif()

if(USENC3 OR USENC4)
  set(USENC ON)
endif()

add_subdirectory(src)
